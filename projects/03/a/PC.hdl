// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    // Put your code here:
    Inc16(in=pcout, out=incout);
    // if inc=1: increment current pc value
    Mux16(a=pcout, b=incout, sel=inc, out=muxinc);
    // if load=1: set current pc value to input value else increment current pc value if inc=1
    Mux16(a=muxinc, b=in, sel=load, out=muxload);
    // if reset=1: set current pc value to 0, else set current pc value to load value if load value=1, else increment current pc value
    Mux16(a=muxload, b=false, sel=reset, out=muxreset);
    Register(in=muxreset, load=true, out=out, out=pcout);
}
